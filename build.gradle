import java.text.SimpleDateFormat

// enable intellij integration & load vmf version to be used
buildscript {

    ext.commonProps = new Properties()
    rootProject.file("config/common.properties").withInputStream { commonProps.load(it) }

    ext.vmfPluginIntelliJIntegration = true

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "eu.mihosoft.vmf:vmf-gradle-plugin:${project.commonProps.get('vmf.version')}"
    }
}

plugins {
    id 'java-library'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.7.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

ext.commonProps = new Properties()
rootProject.file("config/common.properties").withInputStream { commonProps.load(it) }

apply plugin: 'eu.mihosoft.vmf'
apply from: 'gradle/publishing.gradle'

// set dependencies for intellij editor (otherwise vmf dir would be ignored)
idea.module{
    dependencies {
        implementation group: 'eu.mihosoft.vmf', name: 'vmf', version: project.commonProps.get("deps.vmf.version")
    }
}


wrapper {
    gradleVersion = '7.4.2'
}

sourceCompatibility = '11'
targetCompatibility = '11'
[compileJava, compileTestJava]*.sourceCompatibility = '11'
[compileJava, compileTestJava]*.targetCompatibility = '11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"   //<< "-Werror"
    options.compilerArgs << "-Xlint:deprecation" //<< "-Werror"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    implementation "eu.mihosoft.vmf:vmf:${project.commonProps.get('vmf.version')}"

    implementation 'eu.mihosoft.binarytypeutils:binarytypeutils:0.1.3'

    implementation 'org.tinylog:tinylog-api:2.3.2'
    implementation 'org.tinylog:tinylog-impl:2.3.2'

    api 'com.fazecast:jSerialComm:2.9.2'

    api "eu.mihosoft.vmf:vmf-runtime:${project.commonProps.get('vmf.version')}"

//    implementation fileTree(dir: 'C:/Users/miho/Downloads/', include: 'jSerialComm-2.8.0-beta4.jar')
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'eu.mihosoft.devcom')
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true

    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }

    ext.yearCurrent  = new SimpleDateFormat("yyyy").format(new Date())
    ext.yearSince1   = '2019'
    ext.author1 = 'Michael Hoffer <info@michaelhoffer.de>'

    exclude '**/*.svg'
}
